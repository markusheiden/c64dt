plugins {
    id 'com.github.ben-manes.versions'
    id 'io.spring.dependency-management' apply false
}

wrapper {
    gradleVersion = "${gradleVersion}"
}

apply from: 'gradle/javafx.gradle'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(15))
    }
}

allprojects {
    group = 'de.heiden.c64dt'
    version = '1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    archivesBaseName = "${rootProject.name}-${project.name}"

    dependencies {
        implementation "org.slf4j:slf4j-api:${slf4jVersion}"
        runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
        runtimeOnly "org.apache.logging.log4j:log4j-core:${log4jVersion}"

        implementation "org.bitbucket.cowwoc:requirements-core:${requirementsVersion}"

        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
    }

    dependencyManagement {
        dependencies {
            dependency("org.springframework:spring-core:${springVersion}") {
                exclude 'commons-logging:commons-logging'
            }
            dependency "org.springframework:spring-beans:${springVersion}"
            dependency "org.springframework:spring-context:${springVersion}"

            dependency "commons-io:commons-io:${commonsIoVersion}"

            dependency "javax.annotation:javax.annotation-api:${annotationVersion}"
            dependency "javax.xml.bind:jaxb-api:${jaxbVersion}"
            dependency "org.glassfish.jaxb:jaxb-runtime:${glassfishJaxbVersion}"

            //dependency "org.openjfx:javafx-base:${openjfxVersion}:${openjfxPlatform}"
            //dependency "org.openjfx:javafx-controls:${openjfxVersion}:${openjfxPlatform}"
            //dependency "org.openjfx:javafx-graphics:${openjfxVersion}:${openjfxPlatform}"
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                // Needed because otherwise the dependencies are only in the dependency management section of the pom.
                // So gradle users are missing the version in the dependencies section and fail to resolve the dependencies.
                versionMapping {
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
    }
    build.dependsOn publishToMavenLocal

    test {
        // ignore failing tests
        ignoreFailures = true
    }
}
